apply plugin: 'info.solidsoft.pitest.aggregator'

allprojects {
    repositories {
        mavenCentral()
        maven { url = "https://repo.spring.io/snapshot" }
        maven { url = "https://repo.spring.io/milestone" }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pedro00627/crediya-common")
            credentials {
                username = project.findProperty('githubActor') ?: System.getenv("GITHUB_ACTOR") ?: ""
                password = project.findProperty('githubToken') ?: System.getenv("GITHUB_TOKEN") ?: ""
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'info.solidsoft.pitest'

    compileJava.dependsOn validateStructure

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(21))
            vendor.set(JvmVendorSpec.MICROSOFT)
        }
    }

    test {
        useJUnitPlatform()
    }

    if (!project.path.contains(':model') && !project.path.contains(':usecase')) {
        dependencies {
            implementation 'io.projectreactor:reactor-core'
            implementation 'io.projectreactor.addons:reactor-extra'
            implementation "co.com.pragma:crediya-common-starter:${rootProject.ext.commonVersion}"
            implementation "co.com.pragma:crediya-common-security:${rootProject.ext.commonVersion}"
            implementation platform("org.springframework.boot:spring-boot-dependencies:${rootProject.ext.springBootVersion}")

            testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.13.RELEASE'
            testImplementation 'io.projectreactor:reactor-test'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'

            compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            testCompileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            testAnnotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
        }
    }

    if (project.path.contains(':model') || project.path.contains(':usecase')) {
        dependencies {
            implementation platform("org.springframework.boot:spring-boot-dependencies:${rootProject.ext.springBootVersion}")

            implementation 'io.projectreactor:reactor-core'

            compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            testCompileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
            testAnnotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"

            // Agregadas dependencias de test para mÃ³dulos de dominio
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation 'io.projectreactor:reactor-test'
        }
    }

    tasks.withType(Test).configureEach {
        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)) {
            jvmArgs += [
                    "-XX:+AllowRedefinitionToAddDeleteMethods"
            ]
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    pitest {
        targetClasses = ['co.com.pragma.*']
        excludedClasses = []
        excludedTestClasses = []
        pitestVersion = '1.20.1'
        verbose = false
        outputFormats = ['XML', 'HTML']
        threads = 8
        exportLineCoverage = true
        useClasspathFile = true
        timestampedReports = false
        fileExtensionsToFilter.addAll('xml', 'orbit')
        junit5PluginVersion = '1.2.2'
        failWhenNoMutations = false
        jvmArgs = ["-XX:+AllowRedefinitionToAddDeleteMethods"]
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.setRequired true
            xml.setOutputLocation layout.buildDirectory.file("reports/jacoco.xml")
            csv.setRequired false
            html.setOutputLocation layout.buildDirectory.dir("reports/jacocoHtml")
        }
    }
}

jacoco {
    toolVersion = "${rootProject.ext.jacocoVersion}"
    reportsDirectory.set(layout.buildDirectory.dir("reports"))
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn = [test, subprojects.jacocoTestReport]
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.setRequired true
        csv.setRequired false
        html.setRequired true
    }
}

tasks.register('fullQualityReport') {
    group = 'verification'
    description = 'Runs all tests, code coverage, and mutation testing reports.'
    dependsOn jacocoMergedReport, pitestReportAggregate
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

pitestReportAggregate {
    doLast {
        def reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
        def consolidatedReport = new File(reportDir, 'mutations.xml')
        consolidatedReport.withWriter { writer ->
            writer.write("<mutations>\n")
            subprojects.each { subproject ->
                def xmlReport = subproject.layout.buildDirectory.file("reports/pitest/mutations.xml").get().asFile
                if (xmlReport.exists()) {
                    def xmlContent = xmlReport.replaceAll("<\\?xml[^>]*>", "")
                    xmlContent = xmlContent.replaceAll("</?mutations( partial=\"true\")?>", "")
                    writer.write(xmlContent.trim() + "\n")
                }
            }
            writer.write("</mutations>")
        }
    }
}

if (project == rootProject) {
    tasks.named('wrapper') {
        gradleVersion = '9.0.0'
    }
}
